; PROCEDURE COMMENTS +
; NAME: rback_from_MODIS_TS
; AUTHOR: Michele Meroni
; CONTACT INFO: michele.meroni@gmail.com
; DESCRIPTION:
; CALLING SEQUENCE:
;       rback = rback_from_MODIS_TS(R1, R2, R3, R4, R5, R6, R7)
; INPUTS: time series of MODIS reflectances
;         prct: the desired percentile used to retrieve background reflectance
; OUTPUTS: reflectance of the background to be used in PROSAIL (400-2500 nm, 1 nm step)
;          Not that it is assumed that the bare soil is visible during the dry season
; OPTIONAL OUTPUTS:
; OPTIONAL INPUT KEYWORD(S):
; NOTES:
; METHOD:
;  - compute the NDVI time series and extract the timing of the 1st percentile of NDVI time series,
;    the  prct (1-5) instead of the absloute min is taken to avoid outliers
;  - extract the 7 reflectances for that time
;  - fit a 3rd degree polynomium and estimate the reflectance over th 400-2500 nm range
;  Note that the values are used by PROSAIL only within MODIS bands
; EXAMPLE:
; MODIFICATION HISTORY:
;       Coded on 21 July 2014
; CODING NOTES:



FUNCTION rback_from_MODIS_TS, R1, R2, R3, R4, R5, R6, R7, prct
IF (prct LT 0.0) OR (prct GT 10.0) THEN STOP      ;ASKING FOR PRCT GT THAN 10 WOULD strange..
; missing data must have NaN value
maxVal = MAX([R1, R2, R3, R4, R5, R6, R7], /NAN)
minval = MIN([R1, R2, R3, R4, R5, R6, R7], /NAN)
IF (minVal LT 0.0) OR (maxVal GT 1.0) THEN STOP
;compute NDVI
ndvi =  (R2 - R1) / (R2 + R1)
indFin = WHERE(FINITE(ndvi))
indSortFin = SORT(ndvi[indFin])
;Take the observation corresponding to the prct percentile to avoid outlayers 
n = N_ELEMENTS(indSortFin)
scaledPerc = linspace(0, 100, n)
ind = VALUE_LOCATE(scaledPerc, prct)
data_ind = indFin[indSortFin[ind]]

wl = datacentwl_MODIST_7b()
R = [R1[data_ind], R2[data_ind], R3[data_ind], R4[data_ind], R5[data_ind], R6[data_ind], R7[data_ind]]
indwl = SORT(wl)
c = REFORM(POLY_FIT(wl, R, 3, YFIT = Rfit, STATUS = status))
IF (status NE 0) THEN STOP
wlspec = FLOAT(INDGEN(2101) + 400) 
Rfit2 = c[0] + c[1] * wlspec + c[2] * wlspec^2 + c[3] * wlspec^3 
;h = PLOT(wl, R, LINESTYLE = "none", SYMBOL="o")
;h = PLOT(wl[indwl], Rfit[indwl], COLOR="black", OVERPLOT=1)
;h = PLOT(wlspec, Rfit2, COLOR="green", OVERPLOT=1)

;data=dataSpec_P5B() ;;
;Rsoil1 = REFORM(data(9,*))
;Rsoil2 = REFORM(data(10,*))
;h2 = PLOT(wlspec, Rsoil1, OVERPLOT=1, color = 'blue')
;h2 = PLOT(wlspec, Rsoil2, OVERPLOT=1, color = 'red')
RETURN, Rfit2
END